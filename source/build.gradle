ext {
    isCi = 'true' == System.getenv('CI')
    ciBuildNumber = System.getenv('BUILD_NUM')

    workingBranch = {
        'git rev-parse --abbrev-ref HEAD'.execute([], rootDir).text.trim()
    }

    shortSha = {
        'git rev-parse --short HEAD'.execute([], rootDir).text.trim()
    }

    isDirty = {
        'git describe --dirty'.execute([], rootDir).text.trim().contains('dirty')
    }

    isReleaseBuild = {
        workingBranch().equals('master') && !isDirty()
    }

    versionName = {
        def version = '1.2.19'.concat("-${shortSha()}")
        version = isReleaseBuild() ?
                version :
                version.concat(".${workingBranch().replace('/', '.')}".replace('.HEAD', ''))
        if (ciBuildNumber != null) {
            version = version.concat(".${ciBuildNumber}")
        }
        return version
    }
}

apply plugin: 'groovy'
apply plugin: 'com.novoda.bintray-release'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.9.1'
    }
}

repositories {
    jcenter()
    google()
    maven { url "https://plugins.gradle.org/m2/" }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation gradleApi()
    implementation localGroovy()
    implementation "org.javassist:javassist:3.27.0-GA"
    implementation 'com.android.tools.build:gradle:4.1.0'
}

//afterEvaluate {
//    tasks.all { task ->
//        if (task.name == "generatePomFileForMavenPublication") {
//            task.setEnabled(false)
//        } else {
//        }
//    }
//}

publish {
    userOrg = 'kezong'
    groupId = 'com.kezong'
    artifactId = 'fat-aar'
    publishVersion = '1.3.3'
    desc = 'A gradle plugin that merge dependencies into the final aar file works with AGP 3.+'
    website = 'https://github.com/kezong/fat-aar-android'
}

